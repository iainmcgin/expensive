apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "io.demoapp.expensive"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    signingConfigs {
        debugAndRelease {
            storeFile file("${rootDir}/expensive.keystore")
            storePassword "expensive"
            keyAlias "expensive"
            keyPassword "expensive"
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaLangVersion
        targetCompatibility rootProject.ext.javaLangVersion
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.debugAndRelease
        }

        release {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.debugAndRelease
        }
    }

    dataBinding {
        enabled = true
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // App's dependencies, including test
    compile "android.arch.lifecycle:runtime:$rootProject.archVersion"
    compile "android.arch.lifecycle:extensions:$rootProject.archVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.google.firebase:firebase-core:$rootProject.firebaseVersion"
    compile "com.google.firebase:firebase-auth:$rootProject.firebaseVersion"
    compile "com.google.guava:guava:$rootProject.guavaVersion"
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-scalars:$rootProject.retrofitVersion"

    compile "org.openyolo:openyolo-api:$openyoloVersion"
    compile "com.android.support:customtabs:$rootProject.supportLibraryVersion"
    compile "net.openid:appauth:$appauthVersion"

    provided "com.google.auto.value:auto-value:1.4.1"

    annotationProcessor "android.arch.lifecycle:compiler:$archVersion"
    annotationProcessor "com.google.auto.value:auto-value:1.4.1"

    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "org.mockito:mockito-all:$rootProject.mockitoVersion"
    testCompile "org.hamcrest:hamcrest-all:$rootProject.hamcrestVersion"
    testCompile "org.robolectric:robolectric:$rootProject.robolectricVersion"
    testCompile "com.google.guava:guava:$rootProject.guavaVersion"

    testAnnotationProcessor "com.google.auto.value:auto-value:1.4.1"

    // Resolve conflicts between main and test APK:
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$rootProject.supportLibraryVersion"
    androidTestCompile "com.google.guava:guava:$rootProject.guavaVersion"

    androidTestAnnotationProcessor "com.google.auto.value:auto-value:1.4.1"
}

apply plugin: 'com.google.gms.google-services'